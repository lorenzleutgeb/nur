{
  config,
  lib,
  pkgs,
  ...
}: let
  inherit (lib) concatStringsSep mapAttrsToList optionalString concatStrings;

  hostname = "git.lorenz.leutgeb.xyz";

  cgitrcLine = name: value: "${name}=${
    if value == true
    then "1"
    else if value == false
    then "0"
    else toString value
  }";

  mkCgitrc = let
    cfg = config.services.cgit.radicle;
  in
    pkgs.writeText "cgitrc" ''
      # global settings
      ${
        concatStringsSep "\n" (
          mapAttrsToList
          cgitrcLine
          ({virtual-root = "/";} // cfg.settings)
        )
      }
      ${optionalString (cfg.scanPath != null) (cgitrcLine "scan-path" cfg.scanPath)}

      # repository settings
      ${
        concatStrings (
          mapAttrsToList
          (url: settings: ''
            ${cgitrcLine "repo.url" url}
            ${
              concatStringsSep "\n" (
                mapAttrsToList (name: cgitrcLine "repo.${name}") settings
              )
            }
          '')
          cfg.repos
        )
      }

      # extra config
      ${cfg.extraConfig}
    '';

  # sudo -u tor cat /var/lib/tor/onion/radicle/hostname
  onion = "q37hn7rhcbqiirapcx5bkgxzue5oqijdmjv55anfht2ne5hxxjxyhhyd.onion";
  ygg = "[202:f094:502b:1b03:9e0:2c3d:bc8b:428b]";
in {
  environment.etc."cgitrc".source = mkCgitrc;
  systemd.services."fcgiwrap-cgit-radicle".serviceConfig = {
    DynamicUser = lib.mkForce "false";
    User = config.systemd.services.radicle-node.serviceConfig.user or "radicle";
  };
  services = {
    fcgiwrap.instances."cgit-radicle".socket = let
      inherit (config.services) caddy;
    in {
      user = lib.mkForce caddy.user;
      group = lib.mkForce caddy.group;
    };
    cgit.radicle = {
      enable = false;
      scanPath = "/var/lib/radicle/storage";
      #package = pkgs.cgit-pink-radicle;
      settings = {
        enable-git-config = true;
        enable-blame = true;
        enable-commit-graph = false;
        enable-follow-links = true;
        enable-log-filecount = true;
        enable-log-linecount = true;
        enable-remote-branches = true;
        enable-tree-linenumbers = true;
        css = "/x/cgit.css";
        favicon = "/assets/favicon.ico";
        robots = "noindex, nofollow";
        clone-url = "rad:$CGIT_REPO_URL http://${onion}/$CGIT_REPO_URL http://${ygg}/$CGIT_REPO_URL";
        source-filter = "${config.services.cgit.radicle.package}/lib/cgit/filters/syntax-highlighting.py";
        about-filter = builtins.toString (pkgs.writeShellScript "about-filter.sh" ''
          case "$(printf '%s' "$1" | tr '[:upper:]' '[:lower:]')" in
              *.markdown|*.mdown|*.md|*.mkd) FROM='markdown'; ;;
              *.rst) FROM='rst'; ;;
              *.[1-9]) FROM='man'; ;;
              *.htm|*.html) exec cat; ;;
              *.dj) FROM='djot'; ;;
              *.txt|*) FROM='plain'; ;;
          esac

          ${lib.getExe pkgs.pandoc} --from=$FROM --to=html -
        '');
        logo = "/x/radicle.svg";
        #header = builtins.toString (pkgs.writeText "header" "Hey!");
        footer = builtins.toString (pkgs.writeText "footer" ''
          <div class="footer">generated by ${config.services.cgit.radicle.package.name}, ${pkgs.git.name}, radicle-${config.services.radicle.package.version}</div>
        '');
        root-title = "Radicle Repository Browser ðŸ”ŽðŸ“œ";
        root-desc = "";
        snapshots = "tar.gz tar.xz tar.zst zip";
        root-readme = builtins.toString (pkgs.runCommand "root-readme.html" {
            nativeBuildInputs = [pkgs.pandoc];
          } ''
            pandoc --from=markdown --to=html --output=$out ${./about-cgit.md}
          '');
      };
    };

    radicle = {
      enable = true;
      privateKeyFile = "/etc/ssh/ssh_host_ed25519_key";
      publicKey = "/etc/ssh/ssh_host_ed25519_key.pub";
      settings.node = {
        alias = "lorenz.leutgeb.xyz";
        externalAddresses = [
          "${onion}:8776"
          "${ygg}:8776"
        ];
      };
    };

    caddy.virtualHosts = let
      cgit = ''
        handle_path /assets/* {
          root * ${config.services.cgit.radicle.package}/cgit
          file_server
        }
        handle_path /x/* {
          root * /var/www/${hostname}
          file_server
        }
        reverse_proxy unix/${config.services.fcgiwrap.instances."cgit-radicle".socket.address} {
          transport fastcgi {
            env SCRIPT_FILENAME ${config.services.cgit.radicle.package}/cgit/cgit.cgi
          }
        }
        header Onion-Location http://${onion}{path}
      '';
    in {
      ${hostname}.extraConfig = cgit;
      "http://${onion}".extraConfig = cgit;
      "http://${ygg}".extraConfig = cgit;
      "https://git.leutgeb.xyz".extraConfig = ''
               header Content-Type text/html
               header Location http://${onion}{path}
        respond <<HTML
          <!DOCTYPE html>
          <html lang="en"><head><meta charset="utf-8"><title>Multiple Choices</title></head>
          <body><h1>Multiple Choices</h1><p>This service is available via:</p><dl>
          <dt><a href="https://torproject.com">Tor</a></dt>
          <dd><a href="http://${onion}{path}">http://${onion}{path}</a></dd>
          <dt><a href="https://yggdrasil-network.github.io/">Yggdrasil</a></dt>
          <dd><a href="http://${ygg}{path}">http://${ygg}{path}</a></dd>
          </dl></body></html>
          HTML 300
      '';
    };
    nginx.virtualHosts.radicle = lib.mkForce {};
  };
}
